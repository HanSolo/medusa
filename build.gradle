import java.text.SimpleDateFormat

/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2016-2020 Gerrit Grunwald.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Medusa main build file
plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.google.osdetector'           version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.8.7'
    id 'org.beryx.jlink'                 version '2.23.2'
    id 'net.nemerosa.versioning'         version '2.14.0'
    id 'biz.aQute.bnd.builder'           version '5.3.0'
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'Medusa is a JavaFX library containing gauges and clocks'
mainClassName = "$moduleName/eu.hansolo.Medusa.Demo"


Date buildTimeAndDate = new Date()
ext {
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform        = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')

}

project.logger.lifecycle('Platform       : ' + ext.platform)
project.logger.lifecycle('ciOssrhUsername: ' + ext.ciOssrhUsername)
project.logger.lifecycle('ossrhUsername  : ' + ossrhUsername)

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.openjfx:javafx-base:16:$platform"
    implementation "org.openjfx:javafx-graphics:16:$platform"
    implementation "org.openjfx:javafx-controls:16:$platform"
    implementation "org.openjfx:javafx-swing:16:$platform"
}

jar {
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://www.eclipse.org/legal/eplfaq.php',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo',
                'Export-Package'        : 'eu.hansolo.medusa,eu.hansolo.medusa.tools,eu.hansolo.medusa.skins,eu.hansolo.medusa.events'
        )
    }
}

// start the demo from gradle
task Demo(type: JavaExec) {
    main = "eu.hansolo.medusa.DemoLauncher"
    classpath = sourceSets.main.runtimeClasspath
}

// Only used for internal test
task Test(type: JavaExec) {
    main = "eu.hansolo.medusa.TestLauncher"
    classpath = sourceSets.main.runtimeClasspath
}

// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
    sign publishing.publications
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            group = 'eu.hansolo'
            artifactId = 'Medusa'
            version = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }

            pom {
                name = 'Medusa'
                description = 'Medusa is a JavaFX library containing gauges and clocks'
                url = "https://github.com/HanSolo/Medusa/wiki"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "HanSolo"
                        name = "Gerrit Grunwald"
                        email = "han.solo@mac.com"
                    }
                }
                scm {
                    connection = "scm:git:https://HanSolo/Medusa"
                    developerConnection = "scm:git:https://HanSolo/Medusa"
                    url = "https://github.com/HanSolo/Medusa"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = null == ciOssrhUsername ? ossrhUsername : ciOssrhUsername
                password = null == ciOssrhPassword ? ossrhPassword : ciOssrhPassword
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/HanSolo/Medusa"
            credentials {
                username = System.getenv("GH_USER")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
}
